// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1 // if (out < 0) set ng = 1
CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zx -> zero x
    // Use Mux to choose x or false
    Mux16(a=x, b=false, sel=zx, out=xZx);
    // nx -> negate x; negate all bits, not arithmetic negation (multiply by -1)
    // Mux(x_zx, Not16(x)) = x_nx
    // Calculate Not(x_zx), then use Mux to choose between x_zx and Not(x_zx)
    Not16(in=xZx, out=notXZx);
    Mux16(a=xZx, b=notXZx, sel=nx, out=xZxNx);

    // zy -> zero y
    // Use Mux to choose y or false
    Mux16(a=y, b=false, sel=zy, out=yZy);
    // ny -> negate y; negate all bits, not arithmetic negation (multiply by -1)
    // Mux(y, Not16(y)) = y_ny
    Not16(in=yZy, out=notYZy);
    Mux16(a=yZy, b=notYZy, sel=ny, out=yZyNy);

    // f == 0 -> x_zx_nx & y_zy_ny
    And16(a=xZxNx, b=yZyNy, out=xAndY);
    // f == 1 -> x_zx_nx + y_zy_ny
    Add16(a=xZxNx, b=yZyNy, out=xPlusY);
    // choose based on f
    Mux16(a=xAndY, b=xPlusY, sel=f, out=outF);

    // Negate output?
    Not16(in=outF, out=notOutF);
    Mux16(a=outF, b=notOutF, sel=no, out=out);
}
